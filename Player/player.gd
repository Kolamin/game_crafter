extends CharacterBody2D

enum {
	MOVE,
	ATTACK,
	ATTACK2,
	ATTACK3,
	BLOCK,
	SLIDE,
	DAMAGE,
	DEATH
}

const SPEED = 150.0
const JUMP_VELOCITY = -400.0

# Get the gravity from the project settings to be synced with RigidBody nodes.
var gravity = ProjectSettings.get_setting("physics/2d/default_gravity")

@onready var anim = $AnimatedSprite2D
@onready var animPlayer = $AnimationPlayer
@onready var stats = $Stats
@onready var leafs: GPUParticles2D = $Leafs

var state = MOVE
var run_speed = 1
var combo = false
var attack_cooldown = false


var damage_basic = 10
var damage_multiplier = 1
var damage_current
var recovery = false

func _ready() -> void:
	Signals.connect("enemy_atack", Callable(self, "on_damage_recieved"))
	

func _physics_process(delta):
	
	# Add the gravity.
	if not is_on_floor():
		velocity.y += gravity * delta
	
	if velocity.y > 0:
		animPlayer.play("Fall")
	
	Global.player_damage = damage_basic * damage_multiplier
	
	match state:
		MOVE:
			move_state()
		ATTACK:
			attack_state()
		ATTACK2:
			attack2_state()
		ATTACK3:
			attack3_state()
		BLOCK:
			block_state()
		SLIDE:
			slide_state()
		DAMAGE:
			damage_state()
		DEATH:
			death_state()
		
	move_and_slide()
	Global.player_pos = self.position
	#Signals.emit_signal("player_position_update", Global.player_pos)
	
func death_state():
	velocity.x = 0
	anim.play("Death")
	await get_tree().create_timer(1.2).timeout
	queue_free()
	get_tree().change_scene_to_file.call_deferred("res://menu.tscn")

func move_state():
	var direction = Input.get_axis("left", "right")
	if direction:
		velocity.x = direction * SPEED * run_speed
		if velocity.y == 0:
			if run_speed == 1:
				animPlayer.play("Walk")
			else :
				animPlayer.play("Run")
	else:
		velocity.x = move_toward(velocity.x, 0, SPEED)
		if velocity.y == 0:
			animPlayer.play("Idle")
	if direction == -1:
		anim.flip_h = true
		$AttackDirection.rotation_degrees = 180
	elif direction == 1:
		anim.flip_h = false
		$AttackDirection.rotation_degrees = 0
	if Input.is_action_pressed("run") and not recovery:
		run_speed = 1.5
		stats.stamina -= stats.run_cost
	else :
		run_speed = 1
		
	if Input.is_action_pressed("block"):
		stats.stamina_cost = stats.slide_cost
		if velocity.x == 0:
			state = BLOCK
		else:
			stats.stamina_cost = stats.slide_cost
			if stats.stamina > stats.stamina_cost:
				state = SLIDE
			
		
		
	if Input.is_action_just_pressed("attack") and attack_cooldown == false:
		if recovery == false:
			stats.stamina_cost = stats.attack_cost
			if attack_cooldown == false and stats.stamina > stats.stamina_cost:
				state = ATTACK
		
		
	
func block_state():
	velocity.x = 0
	animPlayer.play("Block")
	if Input.is_action_just_released("block") or recovery:
		state = MOVE

func slide_state():
	animPlayer.play("Slide")
	await animPlayer.animation_finished
	state = MOVE

func attack_state():
	stats.stamina_cost = stats.attack_cost
	damage_multiplier = 1
	if Input.is_action_just_pressed("attack") and combo == true and stats.stamina > stats.stamina_cost:
		state = ATTACK2
	velocity.x = 0
	animPlayer.play("Attack")
	await animPlayer.animation_finished
	attack_freeze()
	state = MOVE
	
func attack2_state():
	stats.stamina_cost = stats.attack_cost
	damage_multiplier = 1.2
	if Input.is_action_just_pressed("attack") and combo == true and stats.stamina > stats.stamina_cost:
		state = ATTACK3
	animPlayer.play("Attack2")
	await animPlayer.animation_finished
	state = MOVE

func attack3_state():
	
	damage_multiplier = 2
	animPlayer.play("Attack3")
	await animPlayer.animation_finished
	state = MOVE
	
func combo1():
	combo = true
	await animPlayer.animation_finished
	combo = false

func attack_freeze():
	attack_cooldown = true
	await get_tree().create_timer(0.5).timeout
	attack_cooldown = false

func damage_state():
	
	anim.play("Damage")
	await anim.animation_finished
	state = MOVE

func on_damage_recieved(enemy_damage):
	if state == BLOCK:
		enemy_damage /= 2
	elif state == SLIDE:
		enemy_damage = 0
	else:
		state = DAMAGE
		damage_anim()
	stats.health -= enemy_damage
	if stats.health <= 0:
		stats.health = 0
		state = DEATH
	

func _on_stats_no_stamina() -> void:
	recovery = true
	await get_tree().create_timer(2).timeout
	recovery = false
	
func damage_anim():
	velocity.x = 0
	self.modulate = Color(1, 0, 0,1)
	if $AnimatedSprite2D.flip_h == true:
		velocity.x += 200
	else:
		velocity.x -= 200
	var tween = get_tree().create_tween()
	tween.parallel().tween_property(self, "velocity", Vector2.ZERO, 0.1)
	tween.parallel().tween_property(self, "modulate", Color(1,1,1,1), 0.1)

func steps():
	leafs.emitting = true
	leafs.one_shot = true
	
